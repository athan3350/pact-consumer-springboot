plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "au.com.dius.pact" version "4.2.0"
}


group = 'consumer.pact.springboot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.apache.httpcomponents:fluent-hc:4.5.5"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'com.github.tomakehurst:wiremock:2.27.2'
    testImplementation 'au.com.dius.pact.consumer:java8:4.1.7'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.1.7'

}

test {
    useJUnitPlatform()
}

bootRun{
    standardInput = System.in
}


task copyPacts(type: Copy) {
    description 'Copies the generated Pact json file to the provider resources directory'

    from('build/pacts/')
    into('./src/test/resources/pacts/')
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

//pact {
//	publish {
//		pactDirectory = 'consumer/build/pacts'
//		pactBrokerUrl = 'http://localhost:9292/'
//		pactBrokerUsername = 'demo_user_test'
//		pactBrokerPassword = 'demo_password_test'
//		tags = [getGitBranch(), 'test', 'prod']
//		consumerVersion = getGitHash()
//	}
//}

pact {
    publish {
        pactBrokerUrl = System.getProperty("pactBrokerUrl")
        pactBrokerToken = System.getProperty("pactBrokerToken")
    }
}
